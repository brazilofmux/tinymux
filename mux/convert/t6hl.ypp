%{
#include "omega.h"
#include "t6hgame.h"
int  t6hllex();
void t6hlerror(const char *s);

T6H_LOCKEXP *g_t6hKeyExp = NULL;

%}

%define api.prefix {t6hl}
%debug
%union {
    T6H_LOCKEXP                *ple;
}

%token <ple> EVALLIT ATTRLIT DBREF
%type  <ple> term factor expr nested_r nested_ra
%start start

%%
start
:
    expr                         { g_t6hKeyExp = $1; };
;

expr:
    expr '|' term                { $$ = new T6H_LOCKEXP; $$->SetOr($1,$3);     }
|
    term                         { $$ = $1; }
;

term:
    factor '&' term              { $$ = new T6H_LOCKEXP; $$->SetAnd($1,$3);    }
|
    factor                       { $$ = $1; }
;

factor:
    '!' factor                   { $$ = new T6H_LOCKEXP; $$->SetNot($2);       }
|
    '@' nested_r                 { $$ = new T6H_LOCKEXP; $$->SetIndir($2);     }
|
    '=' nested_ra                { $$ = new T6H_LOCKEXP; $$->SetIs($2);        }
|
    '+' nested_ra                { $$ = new T6H_LOCKEXP; $$->SetCarry($2); }
|
    '$' nested_r                 { $$ = new T6H_LOCKEXP; $$->SetOwner($2);     }
|
    '(' expr ')'                 { $$ = $2; }
|
    ATTRLIT                      { $$ = $1; }
|
    EVALLIT                      { $$ = $1; }
|
    DBREF                        { $$ = $1; }
;

nested_r:
    DBREF                        { $$ = $1; }
|
    '(' nested_r  ')'            { $$ = $2; }
;

nested_ra:
    DBREF                        { $$ = $1; }
|
    ATTRLIT                      { $$ = $1; }
|
    '(' nested_ra  ')'           { $$ = $2; }
;

%%

void t6hlerror(const char *s)
{
}
