%{
#include "omega.h"
#include "t5xgame.h"
int  t5xllex();
void t5xlerror(const char *s);

T5X_LOCKEXP *g_t5xKeyExp = NULL;

%}

%define api.prefix {t5xl}
%debug
%union {
    T5X_LOCKEXP                *ple;
}

%token <ple> EVALLIT ATTRLIT DBREF
%type  <ple> term factor expr nested_r nested_ra
%start start

%%
start
:
    expr                         { g_t5xKeyExp = $1; };
;

expr:
    expr '|' term                { $$ = new T5X_LOCKEXP; $$->SetOr($1,$3);     }
|
    term                         { $$ = $1; }
;

term:
    factor '&' term              { $$ = new T5X_LOCKEXP; $$->SetAnd($1,$3);    }
|
    factor                       { $$ = $1; }
;

factor:
    '!' factor                   { $$ = new T5X_LOCKEXP; $$->SetNot($2);       }
|
    '@' nested_r                 { $$ = new T5X_LOCKEXP; $$->SetIndir($2);     }
|
    '=' nested_ra                { $$ = new T5X_LOCKEXP; $$->SetIs($2);        }
|
    '+' nested_ra                { $$ = new T5X_LOCKEXP; $$->SetCarry($2); }
|
    '$' nested_r                 { $$ = new T5X_LOCKEXP; $$->SetOwner($2);     }
|
    '(' expr ')'                 { $$ = $2; }
|
    ATTRLIT                      { $$ = $1; }
|
    EVALLIT                      { $$ = $1; }
|
    DBREF                        { $$ = $1; }
;

nested_r:
    DBREF                        { $$ = $1; }
|
    '(' nested_r  ')'            { $$ = $2; }
;

nested_ra:
    DBREF                        { $$ = $1; }
|
    ATTRLIT                      { $$ = $1; }
|
    '(' nested_ra  ')'           { $$ = $2; }
;

%%

void t5xlerror(const char *s)
{
}
