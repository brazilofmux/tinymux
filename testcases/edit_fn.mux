#
# edit_fn.mux - Test Cases for edit().
#
# Strategy: Vary three argument with color and UTF-8 in combination with
# expected edit() behaviors.
#
@create test_edit_fn
-
@set test_edit_fn=INHERIT QUIET
-
#
# Beginning of Test Cases
#
&tr.tc000 test_edit_fn=
  @log smoke=Beginning edit() test cases.
-
#
# Test Case #1 - Help file examples.
#
&tr.tc001 test_edit_fn=
  @if strmatch(
        setr(0,sha1(
            [edit(This is a test,is,x)]
            [edit(Atlantic,^,Trans)]
          )
        ),
        0337BC64662CB00DFAE98EB132967118CBE47A9A
      )=
  {
    @log smoke=TC001: edit examples. Succeeded.
  },
  {
    @log smoke=TC001: edit examples. Failed (%q0).
  }
-
#
# Test Case #2 - Regressions (no color).
#
&tr.tc002 test_edit_fn=
  @if strmatch(
        setr(0,sha1(
            [translate(edit(plain,plain,%xrplain),p)]
            [translate(edit(abcdXYZabcd,XYZ,ansi(h,repeat(*,40))),p)]
          )
        ),
        39E84B7C9068D0E5B89D0E0A7F602A558EF2D489
      )=
  {
    @log smoke=TC002: Regressions. Succeeded.
  },
  {
    @log smoke=TC002: Regressions. Failed (%q0).
  }
-
#
# Test Case #3 - Regressions (color).
#
&tr.tc003 test_edit_fn=
  @if strmatch(translate(edit(%xra,b,b),p),a)=
  {
      @log smoke=TC003: Regressions. edit() strips color. Okay.
  },
  {
    @if strmatch(
          setr(0,sha1(
              [translate(edit(|%xr||,|,%r),p)]
              [translate(edit(CONNECTEDCONNECTEDabcdefCONNECTEDCONNECTED,CONNECTED,%xr-),p)]
            )
          ),
          529C98FE0F4AEC3A37243A14BDC0C8511EF92EAB
        )=
    {
      @log smoke=TC003: Regressions. Succeeded.
    },
    {
      @log smoke=TC003: Regressions. Failed (%q0).
    }
  }
--
#
# Test Case #4 - Length of single change
#
&tr.tc004 test_edit_fn=
  @if strmatch(translate(edit(%xra,b,b),p),a)=
  {
      @log smoke=TC004: Single Change. edit() strips color. Okay.
  },
  {
    @if strmatch(
          setr(0,sha1(
              [translate(edit(repeat(a,500),repeat(a,500),%xra),p)]
              [translate(edit(a,a,repeat(%xra,500)),p)]
              [translate(edit(repeat(%xba,500),repeat(a,500),a),p)]
              [translate(edit(%xba,a,repeat(a,500)),p)]
            )
          ),
          CE1B02A3551DCB285F627943FE292C877C2C9955
        )=
    {
      @log smoke=TC004: Single Change. Succeeded.
    },
    {
      @log smoke=TC004: Single Change. Failed (%q0).
    }
  }
-
#
# Test Case #5 - Multiple changes
#
&tr.tc005 test_edit_fn=
  @if strmatch(translate(edit(%xra,b,b),p),a)=
  {
      @log smoke=TC005: Multiple Changes. edit() strips color. Okay.;
      @trig me/tr.done
  },
  {
    @if strmatch(
          setr(0,sha1(
              [translate(edit(repeat(a,500),a,%xrab),p)]
              [translate(edit(repeat(ab,500),ab,%xra),p)]
              [translate(edit(repeat(%xba,500),a,a),p)]
              [translate(edit(repeat(%xbab,500),ab,a),p)]
            )
          ),
          FE10E9EEDA8A5A742255BE48EC27E19E2881E007
        )=
    {
      @log smoke=TC005: Multiple Changes. Succeeded.;
      @trig me/tr.done
    },
    {
      @log smoke=TC005: Multiple Changes. Failed (%q0).;
      @trig me/tr.done
    }
  }
-
&tr.done test_edit_fn=
  @log smoke=End edit() test cases.;
  @notify smoke
-
drop test_edit_fn
-
#
# End of Test Cases
#
